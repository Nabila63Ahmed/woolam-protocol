%% PROTOCOL: NSPK: Needham-Schroeder Public-Key Protocol
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%HLPSL:

role initiator(A, B     : agent,
               Ka, Kb   : public_key,
	       Na	: text,
               SND, RCV : channel (dy))
played_by A def=

  local State : nat,
        X     : text

  init State := 1

  transition
   
    1.  State  = 1 /\ RCV(i) 
    =|> State':= 2 /\ SND({Na.A}_Kb)

    2.  State  = 2 /\ RCV({Na.X'}_Ka)
    =|>	State':= 3 /\ SND({X'}_Kb)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role responder(B, A      : agent,
               Kb, Ka    : public_key,
	       Nb	 : text,
	       SecretMsg : text,
               SND, RCV  : channel (dy)) 
played_by B def=

  local State : nat,
	Y     : text

  init State := 1

  transition 

    1.  State  = 1 /\ RCV({Y'.A}_Kb)
    =|> State':= 2 /\ SND({Y'.Nb}_Ka)

    2.  State  = 2 /\ RCV({Nb}_Kb)
    =|> State':= 3 /\ SND({SecretMsg}_Nb)
		   /\ secret(SecretMsg,secret_id,{B,A})

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role environment() def=

    local Snd, Rcv: channel (dy)

    const i, a, b, c        : agent,
	  ka, kb, kc, ki : public_key,
	  na, na2, nb	 : text,
	  secretMsg      : text,
	  secret_id      : protocol_id

    intruder_knowledge = {i, a, b, c, ka, kb, kc, ki, inv(ki)}

    composition

	initiator(a,i,ka,ki,na,Snd,Rcv)           % Example 5 from script
     /\ responder(a,c,ka,kc,na2,secretMsg,Snd,Rcv)
     /\ responder(b,a,kb,ka,nb,secretMsg,Snd,Rcv)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal

  secrecy_of secret_id

end goal

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

environment()
  
